buildscript {
	repositories {
		jcenter()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven {
			name = 'Forge'
			url = 'https://files.minecraftforge.net/maven/'
		}
		maven {
			name = 'Jitpack'
			url = 'https://jitpack.io/'
		}
	}
	dependencies {
		classpath 'com.github.Earthcomputer:fabric-loom:7b42eb7b'
	}
}

plugins {
	id 'maven-publish'
	id 'com.matthewprenger.cursegradle' version "1.1.2"
}
apply plugin: 'fabric-loom'

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = project.archive_name
version = project.mod_version
group = project.maven_group

def ENV = System.getenv()
version = version + "+" + (ENV.BUILD_NUMBER ? ("build." + ENV.BUILD_NUMBER) : "custom")

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.alphaMcp('2.2a.1', loom.mediafire('http://www.mediafire.com/file/vwgj80p8n1plrlq/mcp22a.zip')) { mappings ->
		mappings.method 'net/minecraft/src/World', '(III)Z', 'func_708_k', 'canSeeTheSky'
	}
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

	compile 'com.google.code.gson:gson:2.8.6'
	compile 'com.google.guava:guava:28.2-jre'
	compile 'commons-io:commons-io:2.5'
	compile 'org.apache.commons:commons-lang3:3.9'
	compile 'org.apache.logging.log4j:log4j-api:2.13.0'
	compile 'org.apache.logging.log4j:log4j-core:2.13.0'
}

minecraft {
	refmapName = 'modmenu.refmap.json'
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

remapJar {
	toMappings = 'official'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}

			afterEvaluate {
				artifact(remapJar) {
					builtBy remapJar
				}
			}
		}
	}

	repositories {
		if (project.hasProperty('mavenPass')) {
			maven {
				url = "http://mavenupload.modmuss50.me/"
				credentials {
					username = "buildslave"
					password = project.getProperty('mavenPass')
				}
			}
		}
	}
}

curseforge {
	if (project.hasProperty('curse_api_key')){
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '308702'
		changelog = 'A changelog can be found at https://github.com/Prospector/ModMenu'
		releaseType = 'release'
		addGameVersion '1.15.2'
		mainArtifact(remapJar) {
			displayName = "ModMenu v$version for $project.minecraft_version"
		}
		afterEvaluate {
			uploadTask.dependsOn("remapJar")
		}
	}
	options{
		forgeGradleIntegration = false
	}
}
